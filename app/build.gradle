plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.compose)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kover)
    alias(libs.plugins.ksp)
}

android {
    namespace 'com.mdevor.littlelemon'
    compileSdk 34

    defaultConfig {
        applicationId "com.mdevor.littlelemon"
        minSdk 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        ksp { arg("room.schemaLocation", "$projectDir/schemas") }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        packagingOptions {
            jniLibs {
                useLegacyPackaging true
            }
        }
    }
    kover {
        reports {
            total {
                filters {
                    excludes {
                        annotatedBy(
                                "androidx.compose.ui.tooling.preview.Preview",
                                "androidx.compose.runtime.Composable"
                        )
                        packages(
                                "com.mdevor.littlelemon.presentation.theme",
                                "com.mdevor.littlelemon.di",
                                "com.mdevor.littlelemon.data.local.roomdatabase",
                        )
                        classes(
                                "com.mdevor.littlelemon.presentation.Main*",
                                "*ComposableSingletons*"
                        )
                    }
                }
                verify {
                    rule {
                        minBound(50)
                    }
                }
                xml {
                    title = "Little Lemon XML"
                    onCheck = true
                    xmlFile = file("coverage_report.xml")
                }
            }
        }
    }
}

dependencies {
    implementation(platform(libs.compose.bom))
    implementation(libs.compose.material3)
    implementation(libs.compose.ui)
    implementation(libs.compose.ui.tooling.preview)
    implementation(libs.activity.compose)
    implementation(libs.lifecycle.viewmodel.compose)
    implementation(libs.lifecycle.runtime.compose)
    implementation(libs.coil.compose)
    implementation(libs.navigation.compose)

    // Room
    implementation(libs.room.runtime)
    implementation(libs.room.ktx)
    ksp(libs.room.compiler)

    // Ktor
    implementation(libs.bundles.ktor.client)

    // Koin
    implementation(libs.bundles.koin.android)

    // UI Tests
    androidTestImplementation(platform(libs.compose.bom))
    androidTestImplementation(libs.compose.ui.test.junit4)
    androidTestImplementation(libs.navigation.testing)
    androidTestImplementation(libs.androidx.test.core)
    androidTestImplementation(libs.mockk.android)
    testImplementation(libs.coil.test)

    // Unit Tests
    testImplementation(libs.junit4)
    testImplementation(libs.bundles.koin.test)
    testImplementation(libs.mockk)
    testImplementation(libs.kotlinx.coroutines.test)
    testImplementation(libs.room.testing)
    implementation(libs.androidx.test.core.ktx)
    implementation(libs.androidx.test.ext.junit.ktx)

    // Debug
    debugImplementation(libs.compose.ui.test.manifest)
    debugImplementation(libs.compose.ui.tooling)
}